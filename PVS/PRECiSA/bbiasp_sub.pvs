bbiasp_sub: THEORY
BEGIN

  IMPORTING interval_ulp_sp,
  	        float_bounded_axiomatic@aerr_ulp__single

  R1,E1,R2,E2: VAR Interval

  aeboundsp_sub_ia(R1,E1,R2,E2): Interval =
    If NonNeg?(E1) AND NonNeg?(E2)
    THEN Add(Add(E1,E2),Div(Ulp_sp(Add(Add(Abs(Sub(R1,R2)),E1),E2)),[|2|]))
    ELSE EmptyInterval
    ENDIF

  re1,re2,re3,re4: VAR RealExpr

  x1,x2,x3,x4: VAR real

  aeboundsp_sub_safe(x1,x2,x3,x4): real =
    IF x2 >= 0 AND x4 >= 0
    THEN aerr_ulp_sub(x1,x2,x3,x4)
    ELSE 1 ENDIF

  %|- AEB_SUB_inclusion_j : PROOF
  %|- (branch (prove-inclusion-judgement) ((grind)))
  %|- QED   AEB_SUB_inclusion_j
  AEB_SUB_inclusion_j: JUDGEMENT
    aeboundsp_sub_ia
    HAS_TYPE (Inclusion4?( Any?,
    	     		   pre1to2b(NonNeg?),
			   Any3?,
			   pre1to4d(NonNeg?),
			   aeboundsp_sub_safe ))

  AEB_SUB_fundmental_j: JUDGEMENT
    aeboundsp_sub_ia
    HAS_TYPE (Fundamental4?(Any?,pre1to2b(NonNeg?), Any3?,pre1to4d(NonNeg?)))

%|- AEB_SUB_fundmental_j : PROOF
%|- (prove-fundamental-judgement)
%|- QED AEB_SUB_fundmental_j

  AEB_SUB_j: JUDGEMENT
    aeboundsp_sub_ia
    HAS_TYPE {F: [[Interval, Interval, Interval, Interval] -> Interval] |
             	   Inclusion4?( Any?,
    	     		   	pre1to2b(NonNeg?),
			   	Any3?,
			   	pre1to4d(NonNeg?),
			   	aeboundsp_sub_safe ) (F)
                   AND Fundamental4?( Any?, pre1to2b(NonNeg?), Any3?,
		       		      pre1to4d(NonNeg?) ) (F)}

  AEB_SUB(re1,re2,re3,re4):  RealExpr =
    FUN4(Any?,pre1to2b(NonNeg?),Any3?,pre1to4d(NonNeg?),
	 aeboundsp_sub_safe,
	 aeboundsp_sub_ia,
	 re1,re2,re3,re4)

END bbiasp_sub
